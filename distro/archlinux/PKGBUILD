pkgname=mate-screensaver
pkgver=2011.11.04
pkgrel=1
pkgdesc='Screensaver for MATE'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'dbus' 'mate-conf' 'mate-desktop' 'mate-menus')
makedepends=('pkgconfig' 'intltool' 'mate-doc-utils')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.10/${pkgname}.tar.gz
		mate-screensaver.pam)
install=mate-screensaver.install
sha256sums=('5b0dabd93635c9cb15bb150cb2f2093232764a97227fe9b4f927db640d35775f'
			'2744d1fc39da46fc681b7cf1c9230d2035b2bb8d9f510213fbe439c114eb76a2')

build() {
	cd ${pkgname}

	./autogen.sh \
		--prefix=/usr --sysconfdir=/etc \
		--mandir=/usr/share \
		--disable-static \
		--with-xscreensaverdir=/usr/share/xscreensaver/config \
		--with-xscreensaverhackdir=/usr/lib/xscreensaver \
		--with-mit-ext \
		--libexecdir=/usr/lib/${pkgname} || return 1

	make || return 1
}

package() {
	cd ${pkgname}

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1
	
	install -m644 "${srcdir}/mate-screensaver.pam" "${pkgdir}/etc/pam.d/mate-screensaver"
	
	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
